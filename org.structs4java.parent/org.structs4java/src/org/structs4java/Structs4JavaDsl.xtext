grammar org.structs4java.Structs4JavaDsl hidden(WS)
generate structs4JavaDsl "http://www.structs4java.org/Structs4JavaDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Package:
	{Package}
	
	SL_COMMENT*
	
	('package' name=QualifiedName ';')?
	
	SL_COMMENT*
	
	(imports += Import)*
	
	(structs += StructDeclaration | enums += EnumDeclaration)*
	
	SL_COMMENT*
;

Import:
    'import' importedNamespace=QualifiedNameWithWildcard ';'
;
	
QualifiedNameWithWildcard:
    QualifiedName '.*'?
;

ComplexTypeDeclaration:
	StructDeclaration | EnumDeclaration
;
	
StructDeclaration:
	{StructDeclaration}
	(comments += SL_COMMENT)*
	'struct' name=ID '{'
		(members += Member)*
		SL_COMMENT*
	'}'
;

Member:
	IntegerMember | FloatMember | StringMember | ComplexTypeMember
;

EnumDeclaration:
	{EnumDeclaration}
	(comments += SL_COMMENT)*
	'enum' name=ID ':' typename=INTEGER_TYPE '{'
		(items += Item)*
		SL_COMMENT*
	'}'
;

Item:
	(comments += SL_COMMENT)*
	name=ID '=' value=INT ','?
;

ComplexTypeMember:
	(comments += SL_COMMENT)*
	type=[ComplexTypeDeclaration|QualifiedName] name=ID
	(array=ArrayDimension)?
	('padding' '(' padding=INT ')')?
	';'
;

IntegerMember:
	(comments += SL_COMMENT)*
	typename=INTEGER_TYPE name=ID 
	(array=ArrayDimension)?
	(
		('sizeof' '(' sizeof=[Member] ')')? &
		('sizeof' '(' sizeofThis?='this' ')')? &
		('countof' '(' countof=[Member] ')')?
	)
	('padding' '(' padding=INT ')')?
	';'
;

FloatMember:
	(comments += SL_COMMENT)*
	typename=FLOAT_TYPE name=ID 
	(array=ArrayDimension)?
	('padding' '(' padding=INT ')')?
	';'
;

StringMember:
	(comments += SL_COMMENT)*
	typename=STRING_TYPE name=ID 
	(array=ArrayDimension)?
	('encoding' '(' encoding=STRING ')')?
	('padding' '(' padding=INT ')')?
	';'
;

ArrayDimension:
	{ArrayDimension} '[' (dimension=INT)? ']'
;

QualifiedName:
    ID ('.' ID)*
;

terminal INT returns ecore::EInt: ('0'..'9')+ | '0x' ('0'..'9' | 'A'..'F' | 'a'..'f')+;
terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		; 
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal INTEGER_TYPE:
	'uint8_t' | 'uint16_t' | 'uint32_t' | 'uint64_t' |
	 'int8_t' | 'int16_t'  | 'int32_t'  | 'int64_t'
;
	
terminal FLOAT_TYPE:
	'float' | 'double'
;

terminal STRING_TYPE:
	'char'
;

terminal ID: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
