grammar org.structs4java.Structs4JavaDsl hidden(WS, ML_COMMENT, SL_COMMENT)
generate structs4JavaDsl "http://www.structs4java.org/Structs4JavaDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Package:
	{Package}
	
	('package' name=QualifiedName ';')?
	
	(imports += Import)*
	
	(structs += StructDeclaration | enums += EnumDeclaration)*
;

Import:
    'import' importedNamespace=QualifiedNameWithWildcard ';'
;
	
QualifiedNameWithWildcard:
    QualifiedName '.*'?
;

ComplexTypeDeclaration:
	StructDeclaration | EnumDeclaration
;
	
StructDeclaration:
	'struct' name=ID '{'
		(members += Member)*
	'}'
;

Member:
	IntegerMember | FloatMember | StringMember | ComplexTypeMember
;

EnumDeclaration:
	'enum' name=ID ':' typename=INTEGER_TYPE '{'
		(items += Item)*
	'}'
;

Item:
	name=ID '=' value=INT ','?
;

ComplexTypeMember:
	type=[ComplexTypeDeclaration|QualifiedName] name=ID
	(array=ArrayDimension)?
	';'
;

IntegerMember:
	typename=INTEGER_TYPE name=ID 
	(array=ArrayDimension)?
	(
		('sizeof' '(' sizeof=[Member] ')')? &
		('sizeof' '(' sizeofThis?='this' ')')? &
		('countof' '(' countof=[Member] ')')?
	)
	';'
;

FloatMember:
	typename=FLOAT_TYPE name=ID 
	(array=ArrayDimension)?
	';'
;

StringMember:
	typename=STRING_TYPE name=ID 
	(array=ArrayDimension)?
	('encoding' '(' encoding=STRING ')')?
	';'
;

ArrayDimension:
	{ArrayDimension} '[' (dimension=INT)? ']'
;

QualifiedName:
    ID ('.' ID)*
;

terminal INT returns ecore::EInt: ('0'..'9')+ | '0x' ('0'..'9' | 'A'..'F' | 'a'..'f')+;
terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal INTEGER_TYPE:
	'uint8_t' | 'uint16_t' | 'uint32_t' | 'uint64_t' |
	 'int8_t' | 'int16_t'  | 'int32_t'  | 'int64_t'
;
	
terminal FLOAT_TYPE:
	'float' | 'double'
;

terminal STRING_TYPE:
	'char'
;

terminal ID: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
