grammar org.structs4java.Structs4JavaDsl with org.eclipse.xtext.common.Terminals

generate structs4JavaDsl "http://www.structs4java.org/Structs4JavaDsl"

Package:
	('package' name=QualifiedName ';')?
	
	(imports += Import)*
	
	('alignment' '(' alignment=INT ')' ';')?
	
	(structs += StructDeclaration | enums += EnumDeclaration)*
;

Import:
    'import' importedNamespace=QualifiedNameWithWildcard
;
	
QualifiedNameWithWildcard:
    QualifiedName '.*'?
;

ComplexTypeDeclaration:
	StructDeclaration | EnumDeclaration
;
	
StructDeclaration:
	'struct' name=ID '{'
		(members += Member)*
	'}'
;

Member:
	IntegerMember | FloatMember | StringMember | ComplexTypeMember
;

EnumDeclaration:
	'enum' name=ID ':' typename=INTEGER_TYPE '{'
		items += Item
		(',' items += Item)*
	'}'
;

Item:
	name=ID '=' value=INT_OR_HEX_INT
;

ComplexTypeMember:
	type=[ComplexTypeDeclaration|QualifiedName] name=ID
	(array=Array)?
	('align' '(' align=INT ')')?
	';'
;

IntegerMember:
	typename=INTEGER_TYPE name=ID 
	(array=Array)?
	(
		('sizeof' '(' sizeof+=[Member] ')')* &
		('sizeof' '(' sizeofThis?='this' ')') &
		('countof' '(' countof+=[Member] ')')* &
		('align' '(' align=INT ')') 
	)
	';'
;

FloatMember:
	typename=FLOAT_TYPE name=ID 
	(array=Array)?
	('align' '(' align=INT ')')?
	';'
;

StringMember:
	typename=STRING_TYPE name=ID 
	(array=Array)?
	(
		('size' '(' size=INT ')') &
		('align' '(' align=INT ')') &
		('encoding' '(' encoding=STRING ')') &
		(nullTerminated ?= 'null-terminated')
	)
	';'
;

Array:
	{Array} '[' (elements=INT)? ']'
;

QualifiedName:
    ID ('.' ID)*
;

terminal INTEGER_TYPE:
	'uint8_t' | 'uint16_t' | 'uint32_t' | 'uint64_t' |
	'int8_t' | 'int16_t' | 'int32_t' | 'int64_t'
;
	
terminal FLOAT_TYPE:
	'float' | 'double'
;

terminal STRING_TYPE:
	'string'
;

terminal INT_OR_HEX_INT:
	INT | HEX_INT
;

terminal HEX_INT:
	'0x' ('0'..'9' | 'A'..'F' | 'a'..'f')+
;

